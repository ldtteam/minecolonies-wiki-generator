plugins {
    id 'java-conventions'
    id 'loader-conventions'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'common')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName].each { variant ->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'common')
            }
        }
    }
}
