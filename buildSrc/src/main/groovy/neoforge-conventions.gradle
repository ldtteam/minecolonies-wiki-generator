import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id 'mc-conventions'
    id 'net.neoforged.gradle.userdev'
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"

    implementation "com.ldtteam:minecolonies:${project.minecolonies_version}"
    implementation "com.ldtteam:structurize:${project.structurize_version}"
    implementation "com.ldtteam:blockui:${project.blockui_version}"
    implementation "com.ldtteam:domum-ornamentum:${project.domum_ornamentum_version}"
    implementation "com.ldtteam:datagenerators:${project.datagenerators_version}:universal"
}

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        workingDirectory project.file('run-data')

        afterEvaluate {
            if (Files.exists(Paths.get(project.projectDir.absolutePath, 'run-data', 'saves', 'datagen_world', 'level.dat'))) {
                args '--quickPlaySingleplayer', 'datagen_world'
            }
        }
    }
}


tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             neo_version            : neo_version,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description,
                             mod_issue_tracker_url  : mod_issue_tracker_url]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}
